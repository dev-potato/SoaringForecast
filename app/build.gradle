apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    //buildToolsVersion "25.0.2" - Android Gradle Plugin now includes default version of build tools
    defaultConfig {
        applicationId "com.fisincorporated.soaringweather"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        //https://artemzin.com/blog/how-to-mock-dependencies-in-unit-integration-and-functional-tests-dagger-robolectric-instrumentation/
        //testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunner "com.fisincorporated.aviationweather.app.OverrideApplicationTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    // To resolve error - DuplicateFileException: Duplicate files copied in APK META-INF/rxjava.properties
    // caused by using RxJava v1 w/ Retrofit v2
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// to pick up mock xml responses
//http://stackoverflow.com/questions/37221590/how-do-i-access-a-text-file-for-junit-test-in-android
// to share test classes/files
//http://blog.danlew.net/2015/11/02/sharing-code-between-unit-tests-and-instrumentation-tests-on-android/
android.sourceSets {
    String sharedTestJava = 'src/sharedTest/java'
    String sharedTestResources = 'src/sharedTest/resources'
    test {
        java.srcDirs += sharedTestJava
        resources.srcDirs += sharedTestResources
    }
    androidTest {
        java.srcDirs += sharedTestJava
        resources.srcDirs += sharedTestResources
    }
}



dependencies {
    final SUPPORT_LIBRARY_VERSION = '26.1.0'
    final CONSTRAINT_LIBRARY_VERSION = '1.0.2'
    final GOOGLE_MAPS_VERSION = '11.6.2'
    final RX_ANDROID = '2.0.2'
    final RX_JAVA = '2.1.12'
    final RETROFIT = '2.2.0'
    final SIMPLE_XML = '2.1.0'
    final GSON = '2.8.2'
    final GSON_CONVERTER = '2.3.0'
    final OKHTTP = '3.8.0'
    final DAGGER = '2.14.1'
    final TIMBER = '4.7.0'
    final ESPRESSO = '2.2.2'
    final HAMCREST = '1.3'
    final RUNNER = '1.0.1'
    final UIAUTOMATOR = '2.1.3'
    final JUNIT = '4.12'
    final MOCKITO = '2.7.6'
    final EVENT_BUS = '3.1.1'
    final MEMORY_CACHE = '1.0.2.Final'
    final DISK_CACHE = '2.0.2'
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Android compat/support libraries
    implementation "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:preference-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support.constraint:constraint-layout:$CONSTRAINT_LIBRARY_VERSION"
    implementation "com.google.android.gms:play-services-maps:$GOOGLE_MAPS_VERSION"


    // Bus
    implementation "org.greenrobot:eventbus:$EVENT_BUS"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$RETROFIT"
    implementation("com.squareup.retrofit2:adapter-rxjava2:$RETROFIT") {
        exclude group: 'io.reactivex.java2', module: 'rxjava'
    }

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$RX_JAVA"
    implementation "io.reactivex.rxjava2:rxandroid:$RX_ANDROID"

    // SimpleXML
    implementation("com.squareup.retrofit2:converter-simplexml:$SIMPLE_XML") {
        exclude module: 'stax-api'
        exclude module: 'stax'
        exclude module: 'xpp3'
    }

    // GSON (JSON)
    implementation "com.google.code.gson:gson:$GSON"
    implementation "com.squareup.retrofit2:converter-gson:$GSON_CONVERTER"

    //okhttp
    implementation "com.squareup.okhttp3:okhttp:$OKHTTP"

    //Dagger2
    implementation "com.google.dagger:dagger:$DAGGER"
    implementation "com.google.dagger:dagger-android:$DAGGER"
    implementation "com.google.dagger:dagger-android-support:$DAGGER"
    annotationProcessor "com.google.dagger:dagger-android-processor:$DAGGER"
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER"

    // Diskcache
    implementation "com.jakewharton:disklrucache:$DISK_CACHE"

    // Memory cache
    implementation "org.cache2k:cache2k-api:$MEMORY_CACHE"
    implementation "org.cache2k:cache2k-core:$MEMORY_CACHE"

    // Timber for logging
    implementation "com.jakewharton.timber:timber:$TIMBER"

    // JVM Unit Testing
    testImplementation "junit:junit:$JUNIT"
    testImplementation "org.mockito:mockito-core:$MOCKITO"

    //Android testing
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$ESPRESSO") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'javax.inject'
    }
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$ESPRESSO") {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$ESPRESSO") {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
    androidTestImplementation "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    androidTestImplementation "com.android.support.test:runner:$RUNNER"
    androidTestImplementation "com.android.support.test:rules:$RUNNER"

    // Matchers
    androidTestImplementation "org.hamcrest:hamcrest-library:$HAMCREST"

    //UI testing with UI Automator
    androidTestImplementation "com.android.support.test.uiautomator:uiautomator-v18:$UIAUTOMATOR"
}


